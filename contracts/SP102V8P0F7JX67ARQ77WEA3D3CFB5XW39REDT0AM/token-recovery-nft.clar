(impl-trait 'SP2PABAF9FTAJYNFZH93XENAJ8FVY99RRM50D2JG9.nft-trait.nft-trait)
(use-trait commission-trait 'SP3D6PV2ACBPEKYJTCMH7HEN02KP87QSP8KTEH335.commission-trait.commission)
(define-non-fungible-token recovery-nft uint)
(define-constant err-not-authorized (err u8000))
(define-constant err-listing (err u8001))
(define-constant err-wrong-commission (err u8002))
(define-constant err-not-found (err u8003))
(define-constant err-metadata-frozen (err u8004))
(define-constant err-transfer-failed (err u8005))
(define-data-var token-nonce uint u0)
(define-data-var ipfs-root (string-ascii 80) "")
(define-data-var metadata-frozen bool false)
(define-data-var transferrable bool false)
(define-map token-count principal uint)
(define-map market uint {price: uint, commission: principal, royalty: uint})
(define-read-only (get-owner (token-id uint))
  (ok (nft-get-owner? recovery-nft token-id)))
(define-read-only (get-last-token-id)
  (ok (var-get token-nonce)))
(define-read-only (get-token-uri (token-id uint))
  (ok (some (concat (concat (var-get ipfs-root) (int-to-ascii token-id)) ".json"))))
(define-read-only (is-dao-or-extension)
	(ok (asserts! (or (is-eq tx-sender .executor-dao) (contract-call? .executor-dao is-extension contract-caller)) err-not-authorized)))
(define-read-only (get-transferrable)
	(var-get transferrable))
(define-read-only (get-balance (account principal))
  (default-to u0
    (map-get? token-count account)))
(define-read-only (get-listing-in-ustx (token-id uint))
  (map-get? market token-id))
(define-public (transfer (token-id uint) (sender principal) (recipient principal))
  (begin
		(asserts! (var-get transferrable) err-transfer-failed)
    (asserts! (or (is-eq tx-sender sender) (is-eq contract-caller sender)) err-not-authorized)
    (asserts! (is-none (map-get? market token-id)) err-listing)
    (trnsfr token-id sender recipient)))
(define-public (list-in-ustx (token-id uint) (price uint) (comm-trait <commission-trait>))
  (let (
			(listing  {price: price, commission: (contract-of comm-trait), royalty: u0}))
		(asserts! (var-get transferrable) err-transfer-failed)
    (asserts! (is-sender-owner token-id) err-not-authorized)
    (map-set market token-id listing)
    (print (merge listing {a: "list-in-ustx", id: token-id}))
    (ok true)))
(define-public (unlist-in-ustx (token-id uint))
  (begin
    (asserts! (is-sender-owner token-id) err-not-authorized)
    (map-delete market token-id)
    (print {a: "unlist-in-ustx", id: token-id})
    (ok true)))
(define-public (buy-in-ustx (token-id uint) (comm-trait <commission-trait>))
  (let (
			(owner (unwrap! (nft-get-owner? recovery-nft token-id) err-not-found))
      (listing (unwrap! (map-get? market token-id) err-listing))
      (price (get price listing))
      (royalty (get royalty listing)))
		(asserts! (var-get transferrable) err-transfer-failed)
    (asserts! (is-eq (contract-of comm-trait) (get commission listing)) err-wrong-commission)		
    (try! (stx-transfer? price tx-sender owner))
    (try! (contract-call? comm-trait pay token-id price))
    (try! (trnsfr token-id owner tx-sender))
    (map-delete market token-id)
    (print {a: "buy-in-ustx", id: token-id})
    (ok true)))
(define-public (set-transferrable (new-transferrable bool))
	(begin 
		(try! (is-dao-or-extension))
		(ok (var-set transferrable new-transferrable))))
(define-public (mint (recipient principal))
  (let (
			(current-balance (get-balance recipient))
			(token-id (+ (var-get token-nonce) u1)))
    (try! (is-dao-or-extension))
    (var-set token-nonce token-id)
    (map-set token-count recipient (+ current-balance u1))
    (try! (nft-mint? recovery-nft token-id recipient))
		(ok token-id)))
(define-public (burn (token-id uint))
  (let (
			(owner (unwrap! (nft-get-owner? recovery-nft token-id) err-not-found))
      (current-balance (get-balance owner)))
    (try! (is-dao-or-extension))
    (asserts! (is-none (map-get? market token-id)) err-listing)
    (try! (nft-burn? recovery-nft token-id owner))
    (map-set token-count owner (- current-balance u1))
    (ok true)))
(define-public (set-base-uri (new-base-uri (string-ascii 80)))
  (begin
    (try! (is-dao-or-extension))
    (asserts! (not (var-get metadata-frozen)) err-metadata-frozen)
    (print { notification: "token-metadata-update", payload: { token-class: "nft", contract-id: (as-contract tx-sender) }})
    (var-set ipfs-root new-base-uri)
    (ok true)))
(define-public (freeze-metadata)
  (begin
    (try! (is-dao-or-extension))
    (var-set metadata-frozen true)
    (ok true)))
		
(define-private (is-owner (token-id uint) (user principal))
    (is-eq user (unwrap! (nft-get-owner? recovery-nft token-id) false)))
(define-private (max (a uint) (b uint)) (if (> a b) a b))
(define-private (trnsfr (token-id uint) (sender principal) (recipient principal))
  (match (nft-transfer? recovery-nft token-id sender recipient)
    success
      (let
        ((sender-balance (get-balance sender))
        (recipient-balance (get-balance recipient)))
          (map-set token-count
            sender
            (- sender-balance u1))
          (map-set token-count
            recipient
            (+ recipient-balance u1))
          (ok success))
    error (err error)))
(define-private (is-sender-owner (token-id uint))
  (let ((owner (unwrap! (nft-get-owner? recovery-nft token-id) false)))
    (or (is-eq tx-sender owner) (is-eq contract-caller owner))))