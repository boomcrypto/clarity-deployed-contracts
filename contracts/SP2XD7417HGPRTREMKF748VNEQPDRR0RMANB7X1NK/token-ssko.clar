(define-constant ERR-NOT-AUTHORIZED (err u1000))
(define-fungible-token bridged-sko)
(define-data-var token-name (string-ascii 32) "SKO")
(define-data-var token-symbol (string-ascii 10) "SKO")
(define-data-var token-uri (optional (string-utf8 256)) (some u"https://cdn.alexlab.co/metadata/token-ssko.json"))
(define-data-var token-decimals uint u8)
(define-read-only (is-dao-or-extension)
	(ok (asserts! (or (is-eq tx-sender .executor-dao) (contract-call? .executor-dao is-extension contract-caller)) ERR-NOT-AUTHORIZED)))
(define-public (set-name (new-name (string-ascii 32)))
	(begin
		(try! (is-dao-or-extension))
		(ok (var-set token-name new-name))))
(define-public (set-symbol (new-symbol (string-ascii 10)))
	(begin
		(try! (is-dao-or-extension))
		(ok (var-set token-symbol new-symbol))
	)
)
(define-public (set-decimals (new-decimals uint))
	(begin
		(try! (is-dao-or-extension))
		(ok (var-set token-decimals new-decimals))))
(define-public (set-token-uri (new-uri (optional (string-utf8 256))))
	(begin
		(try! (is-dao-or-extension))
		(ok (var-set token-uri new-uri))))
(define-public (transfer (amount uint) (sender principal) (recipient principal) (memo (optional (buff 34))))
	(begin
		(asserts! (is-eq sender tx-sender) ERR-NOT-AUTHORIZED)
		(try! (ft-transfer? bridged-sko amount sender recipient))
		(match memo to-print (print to-print) 0x)
		(ok true)))
(define-read-only (get-name)
	(ok (var-get token-name)))
(define-read-only (get-symbol)
	(ok (var-get token-symbol)))
(define-read-only (get-decimals)
	(ok (var-get token-decimals)))
(define-read-only (get-balance (who principal))
	(ok (ft-get-balance bridged-sko who)))
(define-read-only (get-total-supply)
	(ok (ft-get-supply bridged-sko)))
(define-read-only (get-token-uri)
	(ok (var-get token-uri)))
(define-constant ONE_8 u100000000)
(define-public (mint (amount uint) (recipient principal))
	(begin
		(try! (is-dao-or-extension))
		(ft-mint? bridged-sko amount recipient)))
(define-public (burn (amount uint) (sender principal))
	(begin
		(try! (is-dao-or-extension))
		(ft-burn? bridged-sko amount sender)))
(define-private (pow-decimals)
	(pow u10 (unwrap-panic (get-decimals))))
(define-read-only (fixed-to-decimals (amount uint))
	(/ (* amount (pow-decimals)) ONE_8))
(define-private (decimals-to-fixed (amount uint))
	(/ (* amount ONE_8) (pow-decimals)))
(define-read-only (get-total-supply-fixed)
	(ok (decimals-to-fixed (unwrap-panic (get-total-supply)))))
(define-read-only (get-balance-fixed (account principal))
	(ok (decimals-to-fixed (unwrap-panic (get-balance account)))))
(define-public (transfer-fixed (amount uint) (sender principal) (recipient principal) (memo (optional (buff 34))))
	(transfer (fixed-to-decimals amount) sender recipient memo))
(define-public (mint-fixed (amount uint) (recipient principal))
	(mint (fixed-to-decimals amount) recipient))
(define-public (burn-fixed (amount uint) (sender principal))
	(burn (fixed-to-decimals amount) sender))
(define-private (burn-fixed-many-iter (item {amount: uint, sender: principal}))
	(burn-fixed (get amount item) (get sender item)))
(define-public (burn-fixed-many (senders (list 200 {amount: uint, sender: principal})))
	(begin
		(try! (is-dao-or-extension))
		(ok (map burn-fixed-many-iter senders))))